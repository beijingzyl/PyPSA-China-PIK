# SPDX-FileCopyrightText: : 2022 The PyPSA-China Authors
#
# SPDX-License-Identifier: MIT

from os.path import abspath, getctime
from shutil import move
from pathlib import Path
from snakemake.utils import min_version
import logging


# NB: duplicated parameters will be overwritten by the last read cfg
configfile: "config/default_config.yaml"
configfile: "config/technology_config.yaml"
configfile: "config/plot_config.yaml"

# move to path_manager
config["base_results_dir"] = config['results_dir'] + 'version-' + str(config['version'])

include: "rules/postprocess.smk"

ATLITE_NPROCESSES = config['atlite'].get('nprocesses', 4)

storage:
    provider="http",


# PSEUDO RULE TO CALL WHOLE WORKFLOW
if config["foresight"] == "steady-state":

    rule plot_all:
        input:
            # expand(
            #     config["base_results_dir"]+ '/plots/summary/steady_state/postnetwork-{opts}-{topology}-{pathway}-{planning_horizons}_ext.pdf',
            #     **config["scenario"]
            # ),
            expand(
                config["base_results_dir"]+ '/plots/summary/steady_state_{heating_demand}/postnetwork-{opts}-{topology}-{pathway}-pathway_costs.png',
                **{k: v for k, v in config["scenario"].items() if k != "planing_horizons"},
        ),
            # network plot
            expand(
                config["base_results_dir"]+"/plots/steady_state_{heating_demand}/postnetwork-{opts}-{topology}-{pathway}-{planning_horizons}-cost.pdf",      
                **config["scenario"]
            ),

elif config["foresight"] == "myopic":

    rule plot_all:
        input:
            # expand(
            #     config["base_results_dir"]+ '/plots/heatmap/water_tank/water_tank-{opts}-{topology}-{pathway}-{planning_horizons}.png',
            #     ** config["scenario"]
            # ),
            # expand(
            #     config["base_results_dir"]+ '/plots/heatmap/water_tank/water_store-{opts}-{topology}-{pathway}-{planning_horizons}.png',
            #     ** config["scenario"]
            # ),
            expand(
                config["base_results_dir"]+ '/plots/summary/{heating_demand}/postnetwork-{opts}-{topology}-{pathway}-pathway_costs.png',
                **{k: v for k, v in config["scenario"].items() if k != "planing_horizons"},
            ),
            # network plot
            expand(
                config["base_results_dir"]+"/plots/network_{heating_demand}/postnetwork-{opts}-{topology}-{pathway}-{planning_horizons}-cost.pdf",      
                **config["scenario"]
            ),
else:
    raise ValueError("Invalid scenario config: {}".format(config["foresight"]))

rule build_population:
    input:
        # https://www.stats.gov.cn/sj/ndsj/2023/indexeh.htm
        population="resources/data/population/population_from_national_data.csv"
    output:
        population="resources/derived_data/population/population.h5"
    threads: 1
    resources: mem_mb=1000
    script: "scripts/build_population.py"

if config['enable'].get('retrieve_cutout', True):
    rule retrieve_cutout:
        input: storage.http("https://zenodo.org/record/8343761/files/China-2020.nc")
        output: "cutouts/{cutout}.nc"
        run: move(input[0], output[0])


if config['enable'].get('build_cutout', False):
    rule build_cutout:
        input:
            regions_onshore="resources/data/regions/regions_onshore.geojson",
            regions_offshore="resources/data/regions/regions_offshore.geojson"
        output: "cutouts/{cutout}.nc"
        log: "logs/build_cutout/{cutout}.log"
        benchmark: "benchmarks/build_cutout_{cutout}"
        threads: ATLITE_NPROCESSES
        resources: mem_mb=ATLITE_NPROCESSES * 1000
        script: "scripts/build_cutout.py"

rule build_population_gridcell_map:
    input:
        cutout="resources/cutouts/China-2020.nc",
        province_populations="resources/derived_data/population/population.h5",
        #
        population_density_grid="resources/data/population/CFSR_grid.nc",
        # admin 1 level = first administrative level within country
        province_shape="resources/data/province_shapes/CHN_adm1.shp"
    output:
        population_map="resources/derived_data/population/population_gridcell_map.h5"
    log: "logs/build_population_gridcell_map.log"
    threads: 1
    resources: mem_mb=35000
    script: "scripts/build_population_gridcell_map.py"

rule build_solar_thermal_profiles:
    input:
        cutout="resources/cutouts/China-2020.nc",
        population_map="resources/derived_data/population/population_gridcell_map.h5"
    output:
        profile_solar_thermal = f"resources/derived_data/heating/solar_thermal-{config['solar_thermal_angle']}.h5"
    log: "logs/build_solar_thermal_profiles.log"
    threads: 8
    resources: mem_mb=30000
    script: "scripts/build_solar_thermal_profiles.py"

rule build_temp_profiles:
    input:
        population_map="resources/derived_data/population/population_gridcell_map.h5",
        cutout="resources/cutouts/China-2020.nc"
    output:
        temp="resources/derived_data/heating/temp.h5"
    log: "logs/build_temp_profiles.log"
    threads: 8
    resources: mem_mb=30000
    script: "scripts/build_temp_profiles.py"

rule build_cop_profiles:
    input:
        population_map="resources/derived_data/population/population_gridcell_map.h5",
        cutout="resources/cutouts/China-2020.nc",
        temp="resources/derived_data/heating/temp.h5"
    output:
        cop="resources/derived_data/heating/cop.h5"
    threads: 8
    resources: mem_mb=30000
    script: "scripts/build_cop_profiles.py"

if config['enable'].get('retrieve_raster', True):
    rule retrieve_build_up_raster:
        input: storage.http("https://zenodo.org/record/3939050/files/PROBAV_LC100_global_v3.0.1_2019-nrt_BuiltUp-CoverFraction-layer_EPSG-4326.tif")
        output: "resources/data/regions/Build_up.tif"
        run: move(input[0], output[0])
    rule retrieve_Grass_raster:
        input: storage.http("https://zenodo.org/record/3939050/files/PROBAV_LC100_global_v3.0.1_2019-nrt_Grass-CoverFraction-layer_EPSG-4326.tif")
        output: "resources/data/regions/Grass.tif"
        run: move(input[0], output[0])
    rule retrieve_Bare_raster:
        input: storage.http("https://zenodo.org/record/3939050/files/PROBAV_LC100_global_v3.0.1_2019-nrt_Bare-CoverFraction-layer_EPSG-4326.tif")
        output: "resources/data/regions/Bare.tif"
        run: move(input[0], output[0])
    rule retrieve_Shrubland_raster:
        input: storage.http("https://zenodo.org/record/3939050/files/PROBAV_LC100_global_v3.0.1_2019-nrt_Shrub-CoverFraction-layer_EPSG-4326.tif")
        output: "resources/data/regions/Shrubland.tif"
        run: move(input[0], output[0])

rule build_renewable_potential:
    input:
        Build_up_raster="resources/data/landuse_availability/Build_up.tif",
        Grass_raster="resources/data/landuse_availability/Grass.tif",
        Bare_raster="resources/data/landuse_availability/Bare.tif",
        Shrubland_raster="resources/data/landuse_availability/Shrubland.tif",
        # natural reserves data
        natura1='resources/data/landuse_availability/WDPA_WDOECM_Oct2024_Public_CN_shp_0/WDPA_WDOECM_Oct2024_Public_CN_shp-polygons.shp',
        natura2='resources/data/landuse_availability/WDPA_WDOECM_Oct2024_Public_CN_shp_1/WDPA_WDOECM_Oct2024_Public_CN_shp-polygons.shp',
        natura3='resources/data/landuse_availability/WDPA_WDOECM_Oct2024_Public_CN_shp_2/WDPA_WDOECM_Oct2024_Public_CN_shp-polygons.shp',
        # GEBCO bathymetry (water depth)
        gebco="resources/data/landuse_availability/GEBCO_tiff/gebco_2024_CN.tif",
        provinces_shp="resources/data/province_shapes/CHN_adm1.shp",
        offshore_province_shapes="resources/data/regions/regions_offshore_province.geojson",
        offshore_shapes="resources/data/regions/regions_offshore.geojson",
        cutout= "resources/cutouts/China-2020.nc"
    output:
        solar_profile="resources/profile_solar.nc",
        onwind_profile="resources/profile_onwind.nc",
        protected_areas_offshore = "resources/derived_data/landuse_availability/protected_areas_offshore.shp",
        offwind_profile="resources/profile_offwind.nc"
    log: "logs/build_renewable_potential.log"
    threads: ATLITE_NPROCESSES
    resources: mem_mb=ATLITE_NPROCESSES * 5000
    script: "scripts/build_renewable_potential.py"

rule build_load_profiles:
    input:
        population = "resources/derived_data/population/population.h5",
        population_map = "resources/derived_data/population/population_gridcell_map.h5",
        cutout = "resources/cutouts/China-2020.nc",
        intraday_profiles="resources/data/heating/heat_load_profile_DK_AdamJensen.csv",
        space_heat_demand="resources/data/heating/SPH_2020.csv"
    output:
        heat_demand_profile = "resources/derived_data/heating/heat_demand_profile_{heating_demand}_{planning_horizons}.h5",
        energy_totals_name = "resources/derived_data/energy_totals_{heating_demand}_{planning_horizons}.h5"
    log: "logs/build_load_profiles/build_{heating_demand}_{planning_horizons}.log"
    threads: ATLITE_NPROCESSES
    resources: mem_mb = ATLITE_NPROCESSES * 5000
    script: "scripts/build_load_profiles.py"

rule build_biomass_potential:
    input:
        # from doi.org/10.1038/s41467-021-23282-x
        biomass_feedstocks = "resources/data/p_nom/41467_2021_23282_MOESM4_ESM.xlsx"
    log:
        "logs/build_biomass_potential.log"
    output:
        biomass_potential = "resources/derived_data/p_nom/biomass_potential.h5"
    threads: ATLITE_NPROCESSES
    resources: mem_mb = ATLITE_NPROCESSES * 5000
    script: "scripts/build_biomass_potential.py"


if config["foresight"] == "steady-state":
    rule prepare_networks:
        input:
            population_name="resources/derived_data/population/population.h5",
            solar_thermal_name="resources/derived_data/heating/solar_thermal-{angle}.h5".format(angle=config['solar_thermal_angle']),
            heat_demand_name="resources/derived_data/heating/heat_demand_profile_{heating_demand}_{planning_horizons}.h5",
            cop_name="resources/derived_data/heating/cop.h5",
            energy_totals_name="resources/derived_data/energy_totals_{heating_demand}_{planning_horizons}.h5",
            temp="resources/derived_data/heating/temp.h5",
            intraday_profiles="resources/data/heating/heat_load_profile_DK_AdamJensen.csv",
            tech_costs = "resources/data/costs/costs_{planning_horizons}.csv",
            province_shape="resources/data/province_shapes/CHN_adm1.shp",
            central_fraction="resources/data/heating/DH_city_town_2020.h5",
            **{f"profile_{tech}": f"resources/profile_{tech}.nc"
               for tech in config['renewable']}
        output:
            network_name=config["base_results_dir"]+ '/steady_state_{heating_demand}/prenetworks/prenetwork-{opts}-{topology}-{pathway}-{planning_horizons}.nc',
        threads: 1
        resources: mem_mb=10000
        script: "scripts/prepare_network.py"

    rule solve_networks:
        params:
            solving = config["solving"],
        input:
            network_name=config["base_results_dir"]+ '/steady_state_{heating_demand}/prenetworks/prenetwork-{opts}-{topology}-{pathway}-{planning_horizons}.nc',
        output:
            network_name=config["base_results_dir"]+ '/steady_state_{heating_demand}/postnetworks/postnetwork-{opts}-{topology}-{pathway}-{planning_horizons}.nc'
        log:
            solver = "logs/solve_network/steady_state_{heating_demand}_postnetworks/postnetwork-{opts}-{topology}-{pathway}-{planning_horizons}.log"
        threads: 4
        resources: mem_mb=35000
        script: "scripts/solve_network.py"

elif config["foresight"] == "myopic":
    rule prepare_base_networks_2020:
        input:
            overrides = "resources/data/override_component_attrs",
            edges= "resources/data/grids/edges.txt",
            edges_ext = "resources/data/grids/edges_current.csv",
            solar_thermal_name="resources/derived_data/heating/solar_thermal-{angle}.h5".format(angle=config['solar_thermal_angle']),
            cop_name="resources/derived_data/heating/cop.h5",
            province_shape="resources/data/province_shapes/CHN_adm1.shp",
            elec_load="resources/data/load/load_{planning_horizons}_weatheryears_1979_2016_TWh.h5",
            heat_demand_profile= "resources/derived_data/heating/heat_demand_profile_{heating_demand}_{planning_horizons}.h5",
            # TODO: metadata for file, why does it need to be h5?
            central_fraction="resources/data/heating/DH_city_town_2020.h5",
            tech_costs= "resources/data/costs/costs_{planning_horizons}.csv",
            **{f"profile_{tech}": f"resources/profile_{tech}.nc"
               for tech in config['renewable']}
        output:
            network_name=config["base_results_dir"]+ '/prenetworks/{heating_demand}/prenetwork-{opts}-{topology}-{pathway}-{planning_horizons}.nc',
        wildcard_constraints:
            planning_horizons=2020 #only applies to baseyear
        threads: 1
        resources: mem_mb=10000
        script: "scripts/prepare_base_network_2020.py"

    rule prepare_base_networks:
        input:
            overrides = "resources/data/override_component_attrs",
            edges = "resources/data/grids/edges.txt",
            solar_thermal_name="resources/derived_data/heating/solar_thermal-{angle}.h5".format(angle=config['solar_thermal_angle']),
            cop_name="resources/derived_data/heating/cop.h5",
            province_shape="resources/data/province_shapes/CHN_adm1.shp",
            elec_load="resources/data/load/load_{planning_horizons}_weatheryears_1979_2016_TWh.h5",
            heat_demand_profile= "resources/derived_data/heating/heat_demand_profile_{heating_demand}_{planning_horizons}.h5",
            central_fraction="resources/data/heating/DH_city_town_2020.h5",
            tech_costs= "resources/data/costs/costs_{planning_horizons}.csv",
            biomass_potental = "resources/derived_data/p_nom/biomass_potential.h5",
            **{f"profile_{tech}": f"resources/profile_{tech}.nc"
               for tech in config['renewable']}
        output:
            network_name=config["base_results_dir"]+ '/prenetworks/{heating_demand}/prenetwork-{opts}-{topology}-{pathway}-{planning_horizons}.nc',
        threads: 1
        resources: mem_mb=10000
        script: "scripts/prepare_base_network.py"

    ruleorder: prepare_base_networks_2020 > prepare_base_networks

    rule add_existing_baseyear:
        input:
            overrides="resources/data/override_component_attrs",
            network=config["base_results_dir"]+ '/prenetworks/{heating_demand}/prenetwork-{opts}-{topology}-{pathway}-{planning_horizons}.nc',
            tech_costs="resources/data/costs/costs_{planning_horizons}.csv",
            cop_name="resources/derived_data/heating/cop.h5",
            **{f"existing_{tech}": f"resources/data/existing_infrastructure/{tech} capacity.csv"
               for tech in config['existing_infrastructure']},
        output: config["base_results_dir"]+ '/prenetworks-brownfield/{heating_demand}/prenetwork-{opts}-{topology}-{pathway}-{planning_horizons}.nc'
        wildcard_constraints:
            planning_horizons=config['scenario']['planning_horizons'][0] #only applies to baseyear
        threads: 1
        resources: mem_mb=2000
        script: "scripts/add_existing_baseyear.py"

    def solved_previous_horizon(wildcards):
        planning_horizons = config["scenario"]["planning_horizons"]
        i = planning_horizons.index(int(wildcards.planning_horizons))
        planning_horizon_p = str(planning_horizons[i-1])
        return config["base_results_dir"]+ "/postnetworks/{heating_demand}/postnetwork-{opts}-{topology}-{pathway}-" + planning_horizon_p + ".nc"

    rule add_brownfield:
        input:
            overrides="resources/data/override_component_attrs",
            network=config["base_results_dir"]+ '/prenetworks/{heating_demand}/prenetwork-{opts}-{topology}-{pathway}-{planning_horizons}.nc',
            network_p=solved_previous_horizon,#solved network at previous time step
            costs="resources/data/costs/costs_{planning_horizons}.csv",
            **{f"profile_{tech}": f"resources/profile_{tech}.nc"
                for tech in config['renewable']}
        output:
            network_name = config["base_results_dir"]+ '/prenetworks-brownfield/{heating_demand}/prenetwork-{opts}-{topology}-{pathway}-{planning_horizons}.nc',
        threads: 4
        resources: mem_mb=10000
        script: "scripts/add_brownfield.py"

    ruleorder: add_existing_baseyear > add_brownfield

    rule solve_network_myopic:
        params:
            solving = config["solving"],
        input:
            overrides = "resources/data/override_component_attrs",
            network=config["base_results_dir"]+ '/prenetworks-brownfield/{heating_demand}/prenetwork-{opts}-{topology}-{pathway}-{planning_horizons}.nc',
            costs="resources/data/costs/costs_{planning_horizons}.csv",
            biomass_potental= "resources/derived_data/p_nom/biomass_potential.h5",
        output:
            network_name = config["base_results_dir"]+ '/postnetworks/{heating_demand}/postnetwork-{opts}-{topology}-{pathway}-{planning_horizons}.nc'
        log:
            solver = "logs/solve_network_myopic/{heating_demand}/postnetwork-{opts}-{topology}-{pathway}-{planning_horizons}.log"
        threads: 4
        resources: mem_mb = 80000
        script: "scripts/solve_network_myopic.py"


# runme with snakemake --allowed-rules dag
rule dag:
    message:
        "Creating DAG of workflow."
    output:
        dag_dot=f"results/dag/dag.dot",
        dag_pdf="results/dag/dag.pdf",
        dag_png="results/dag/dag.png",
        rules_dot="results/dag/rules_graph.dot",
        rules_pdf="results/dag/rules_graph.pdf",
        rules_png="results/dag/rules_graph.png",
    shell:
        r"""
        snakemake --dag plot_all | sed -n "/digraph/,\$p" > {output.dag_dot}
        dot -Tpdf -o {output.dag_pdf} {output.dag_dot}
        dot -Tpng -o {output.dag_png} {output.dag_dot}
        snakemake --rulegraph plot_all | sed -n "/digraph/,\$p" > {output.rules_dot}
        dot -Tpdf -o {output.rules_pdf} {output.rules_dot}
        dot -Tpng -o {output.rules_png} {output.rules_dot}
        """

def copy_sm_log():
    # Set the source and destination directories
    SOURCE_DIR = ".snakemake/logs"
    DEST_DIR = "logs/"
    NEW_NAME = "latest_snakemake.log"  # The desired new filename

    try:
        logs_sm = Path(".snakemake/log").rglob("*.log")
        newest_log = max(logs_sm, key=getctime)
        destination_file = Path(DEST_DIR) / NEW_NAME
        # Copy and rename the newest file to the destination directory
        shutil.copy(newest_log, destination_file)
    except FileNotFoundError as e:
        pass
    except Exception as e:
        pass

onerror:
    copy_sm_log()

onsuccess:
    copy_sm_log()
